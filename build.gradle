plugins {
	id 'java'
	id 'application'
}

defaultTasks 'clean', 'assemble', 'distZip'

description = 'Structurizr CLI'
group = 'com.structurizr'
version = ''

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {

	implementation "com.structurizr:structurizr-dsl:${structurizrVersion}"
	implementation "com.structurizr:structurizr-export:${structurizrVersion}"
	implementation "com.structurizr:structurizr-autolayout:${structurizrVersion}"
	implementation "com.structurizr:structurizr-inspection:${structurizrVersion}"
//	implementation 'io.github.goto1134:structurizr-d2-exporter:1.5.3'

	implementation 'commons-cli:commons-cli:1.10.0'

	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.25'
	implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.9.25'
	implementation 'org.jruby:jruby-core:9.4.12.1'

	def log4jVersion = '2.25.2'
	implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-jcl:${log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'

}

test {
	useJUnitPlatform()
}

def command(String... command) {
    try {
        return providers.exec {
            commandLine command
        }.standardOutput.asText.get().trim()
    }
    catch (e) {
        e.printStackTrace();
        return "";
    }
}

def getVersionName = { ->
    command 'git', 'describe', '--tags', '--dirty'
}

def getTimestamp = { ->
    command 'date', '--utc', '+%FT%H:%M:%SZ'
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources']
	include 'build.properties'
	include 'static.zip'
}

processResources {
    filesMatching('**/*.properties') {
        filter { line ->
            line.replaceAll('@BUILD_NUMBER@', getVersionName())
                .replaceAll('@BUILD_DATE@', getTimestamp())
        }
    }
}

application {
    mainClass = 'com.structurizr.cli.StructurizrCliApplication'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version
        )
    }
}
