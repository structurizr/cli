plugins {
	id 'java'
}

defaultTasks 'clean', 'build', 'getDeps', 'buildZip'

description = 'Structurizr CLI'
group = 'com.structurizr'
version = ''

sourceCompatibility = 17
targetCompatibility = 17

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {

	implementation "com.structurizr:structurizr-dsl:${structurizrVersion}"
	implementation "com.structurizr:structurizr-export:${structurizrVersion}"
	implementation "com.structurizr:structurizr-autolayout:${structurizrVersion}"
	implementation "com.structurizr:structurizr-inspection:${structurizrVersion}"
	implementation 'io.github.goto1134:structurizr-d2-exporter:1.6.0'

	implementation 'commons-cli:commons-cli:1.9.0'

	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.22'
	implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.9.25'
	implementation 'org.jruby:jruby-core:9.4.8.0'

	def log4jVersion = '2.24.1'
	implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-jcl:${log4jVersion}"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'

}

test {
	useJUnitPlatform()
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources']
	include 'build.properties'
	include 'static.zip'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version
        )
    }
}

task getDeps(type: Copy) {
	from configurations.default
	into 'build/dependencies'
}

task buildZip(type: Zip) {
	from ('build/libs/') {
		include 'structurizr-cli*.jar'
		into 'lib'
	}
	from ('build/dependencies') {
		include '*.jar'
		into 'lib'
	}
	from ('.') {
		include 'README.md'
		include 'docs/*'
		include 'docs/images/*'
	}
	from ('etc') {
		include 'structurizr.sh'
		include 'structurizr.bat'
	}
}
