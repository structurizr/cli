plugins {
	id 'java'
}

defaultTasks 'clean', 'build', 'getDeps', 'buildZip'

description = 'Structurizr CLI'
group = 'com.structurizr'
version = '1.30.0'

sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {

	implementation 'com.structurizr:structurizr-dsl:1.30.0'
	implementation 'com.structurizr:structurizr-export:1.14.0'
	implementation 'io.github.goto1134:structurizr-d2-exporter:1.4.0'
	implementation 'com.structurizr:structurizr-graphviz:2.0.0'

	implementation 'commons-cli:commons-cli:1.5.0'
	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.13'
    implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.7.22'
    implementation 'org.jruby:jruby-core:9.4.0.0'

	implementation 'commons-logging:commons-logging:1.2'
	implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-jcl:2.19.0'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'

}

test {
	useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': version
        )
    }
}

task getDeps(type: Copy) {
	from configurations.default
	into 'build/dependencies'
}

task buildZip(type: Zip) {
	from ('build/libs/') {
		include 'structurizr-cli*.jar'
		into 'lib'
	}
	from ('build/dependencies') {
		include '*.jar'
		into 'lib'
	}
	from ('.') {
		include 'README.md'
		include 'docs/*'
		include 'docs/images/*'
	}
	from ('etc') {
		include 'structurizr.sh'
		include 'structurizr.bat'
	}
}
